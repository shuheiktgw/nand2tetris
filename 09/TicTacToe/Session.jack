class Session {
  field Player player1;
  field Player player2;
  field Array games;
  field int currentGameIdx;
  field boolean isContinue;

  constructor Session new(){
    var String userName1;
    var String userName2;
    var Game newGame;

    do Output.moveCursor(2, 0);
    do Output.printString("Welcome to TicTacToe!");
    do Output.println();

    let userName1 = Keyboard.readLine("Please type first user's name: ");
    let userName2 = Keyboard.readLine("Please type second user's name: ");

    let player1 = Player.new(1, userName1);
    let player2 = Player.new(2, userName2);

    let newGame  = Game.new(player1, player2, Board.new());
    let games = Array.new(100);
    let currentGameIdx = 0;
    let games[currentGameIdx] = newGame;

    let isContinue = true;

    return this;
  }

  method void start() {
    var int continue;
    var Board newBoard;
    var Game newGame;
    var Game cg;

    let cg = currentGame();
    do cg.play();

    let continue = Keyboard.readInt("Do you want to start a new game?(1: yes): ");

    if (continue = 1){
      let newBoard = Board.new();
      let newGame = Game.new(player1, player2, newBoard);
      let currentGameIdx = currentGameIdx + 1;
      let games[currentGameIdx] = newGame;
      do Screen.clearScreen();
      do Output.moveCursor(2, 0);
      do start();
    } else {
      let isContinue = false;
      do Screen.clearScreen();
      do Output.moveCursor(0, 0);

      do Output.printString("Game is over.");
      do Output.println();
      do player1.printGameCount();
      do player2.printGameCount();
    }

    return;
  }

  method Game currentGame(){
    return games[currentGameIdx];
  }

  method boolean getIsContinue(){
    return isContinue;
  }
}
