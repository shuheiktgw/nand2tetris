class Game {
  field Player p1;
  field Player p2;
  field Board board;

  constructor Game new(Player player1, Player player2, Board gameBoard) {
    let p1 = player1;
    let p2 = player2;
    let board = gameBoard;
    return this;
  }

  method void play(){
    var int move1;
    var int move2;

    do board.draw();

    while(board.getEndStatus() = 0) {
      do board.draw();

      let move1 = Keyboard.readInt("Please type first user's move(0-8): ");
      while(~(board.playFirst(move1))) {
        do Output.printString("Invalid move");
        do Output.println();
        let move1 = Keyboard.readInt("Please type first user's move(0-8) again: ");
      }

      do board.draw();

      if (~(board.getEndStatus() = 0)){
        do setResult();
        return;
      }

      let move2 = Keyboard.readInt("Please type second user's move(0-8): ");

      while(~(board.playSecond(move2))) {
        do Output.printString("Invalid move");
        do Output.println();
        let move2 = Keyboard.readInt("Please type second user's move(0-8) again: ");
      }

      do board.draw();
    }

    do setResult();
    return;
  }

  method void setResult(){
    if(board.getEndStatus() = 3) {
      do Output.printString("Game result: The game was even.");
      do Output.println();
    }

    if(board.getEndStatus() = 2) {
      do Output.printString("Game result: Plyer ");
      do Output.printString(p2.getName());
      do Output.printString(" won.");
      do Output.println();

      do p2.incrementWin();
      do p1.incrementLose();
    }

    if(board.getEndStatus() = 1) {
      do Output.printString("Game result: Player ");
      do Output.printString(p1.getName());
      do Output.printString(" won.");
      do Output.println();

      do p1.incrementWin();
      do p2.incrementLose();
    }

    return;
  }
}
